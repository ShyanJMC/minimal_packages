# Maintainer: Joaquin 'ShyanJMC' Crespo <shyan@shyanjmc.com>
# Based on the work on Arch Linux

pkgbase=wireplumber
pkgname=(
  wireplumber
  libwireplumber
)

pkgver=0.4.17
pkgrel=1
pkgdesc="Session / policy manager implementation for PipeWire"
url="https://pipewire.pages.freedesktop.org/wireplumber/"
arch=(x86_64)
license=(MIT)
makedepends=(
  doxygen
  git
  glib2
  gobject-introspection
  graphviz
  meson
  mold
  pipewire
  python-lxml
  systemd
)
checkdepends=(pipewire-audio)
source=("https://gitlab.freedesktop.org/pipewire/wireplumber/-/archive/$pkgver/wireplumber-$pkgver.tar.bz2")
b2sums=('SKIP')

prepare() {
  cd $pkgbase-$pkgver
  mkdir -p build
}

build() {
  local meson_options=(
    -D daemon=true
    -D dbus-tests=false
    -D doc=disabled
    -D elogind=disabled
    -D modules=true
    -D systemd=enabled
    -D system-lua=false
    -D systemd-system-service=false
    -D systemd-user-service=true
    -D tests=false
    -D tools=true
  )

  meson setup --prefix /usr --libexecdir lib --sbindir bin --buildtype plain -D b_lto=true -D b_pie=true -D python.bytecompile=1 $pkgbase-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

_ver=${pkgver:0:3}

package_wireplumber() {
  depends=(
    "libwireplumber=$pkgver-$pkgrel"
    libsystemd.so
    pipewire
  )
  provides=(pipewire-session-manager)
  conflicts=(pipewire-media-session)
  install=wireplumber.install

  meson install -C build --destdir "$pkgdir"

  (
    cd "$pkgdir"

    _pick libw usr/lib/libwireplumber-$_ver.so*
    _pick libw usr/lib/girepository-1.0
    _pick libw usr/lib/pkgconfig
    _pick libw usr/include
    _pick libw usr/share/gir-1.0
  )

}

package_libwireplumber() {
  pkgdesc+=" - client library"
  depends=(
    libg{lib,module,object,io}-2.0.so
    libpipewire-0.3.so
  )
  provides=(libwireplumber-$_ver.so)

  mv libw/* "$pkgdir"

}
