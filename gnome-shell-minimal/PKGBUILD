# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Flamelab <panosfilip@gmail.com

# Modder: Joaquin 'ShyanJMC' Crespo <shyan@shyanjmc.com>

pkgname=gnome-shell
pkgver=45.0
pkgrel=1
epoch=1
pkgdesc="Next generation desktop shell"
url="https://wiki.gnome.org/Projects/GnomeShell"
arch=(x86_64)
license=(GPL)

# gjs -> Gnome JavaScript, a must for gnome-shell
# gtk4 -> the GUI toolkit
# gnome-settings-daemon -> the name explains it self
# mutter -> the window manager. Each gnome-shell release requiere the same version in mutter
# libnma -> Lib Network Manager applet
# accountsservice and libibus -> the integration with DBus 
depends=(accountsservice gcr-4 gjs upower gtk4
         gnome-settings-daemon gsettings-desktop-schemas libcanberra
         libsecret mutter libnma libnma-gtk4 libibus gnome-disk-utility
         #libgdm -> used as support in gnome-shell to logout or change user with gdm
         #gnome-session -> used to specify gnome in gdm
         )
makedepends=(startup-notification evolution-data-server gnome-control-center cmake gobject-introspection git meson mold sassc asciidoc bash-completion)
checkdepends=(xorg-server-xvfb)
optdepends=('gnome-control-center: System settings'
            'evolution-data-server: Evolution calendar integration'
            'gst-plugins-good: Screen recording'
            'gst-plugin-pipewire: Screen recording'
            'gnome-bluetooth-3.0: Bluetooth support'
            'power-profiles-daemon: Power profile switching')
groups=(gnome)

source=("https://gitlab.gnome.org/GNOME/gnome-shell/-/archive/${pkgver}/gnome-shell-${pkgver}.tar.bz2"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git")
        
sha256sums=('fdd06050504a2c7b928d6aeded8137dba279616f9b3fe96c9b87270248c5b864' #gnome-shell-v45.0.tar.bz2
            'SKIP')

prepare() {
  cd gnome-shell-${pkgver}

  git submodule init
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git -c protocol.file.allow=always submodule update
  mkdir -p "$srcdir"/gnome-shell-${pkgver}/subprojects/gvc
  cp "$srcdir"/libgnome-volume-control/* "$srcdir"/gnome-shell-${pkgver}/subprojects/gvc/
}

build() {
  CFLAGS="${CFLAGS/-O2/-O} -fno-semantic-interposition"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"

  arch-meson build gnome-shell-${pkgver} -D camera_monitor=true -D extensions_tool=false -D extensions_app=true -D gtk_doc=false -D man=false -D tests=false -D networkmanager=true 
  meson compile -C build
}

_check() (
  export XDG_RUNTIME_DIR="$PWD/runtime-dir"
  mkdir -p -m 700 "$XDG_RUNTIME_DIR"

  meson test -C build --print-errorlogs
)

check() {
  dbus-run-session xvfb-run -s '-nolisten local +iglx -noreset' \
    bash -c "$(declare -f _check); _check"
}

package() {
  cd gnome-shell-${pkgver}
  depends+=(libmutter-13.so)
  meson install -C ../build --destdir "$pkgdir"
}
