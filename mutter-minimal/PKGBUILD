# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>

# Modder: Joaquin 'ShyanJMC' Crespo <shyanjmc@protonmail.com>
# Note; if compilation fails linking to libdefault.so do in src/build directory; meson setup --wipe
# and then rerun doing; makepkg -sfe to rebuild and will do it fine

pkgbase=mutter
pkgname=(mutter)
pkgver=45.0
pkgrel=1
pkgdesc="Window manager and compositor for GNOME"
url="https://gitlab.gnome.org/GNOME/mutter"
arch=(x86_64)
license=(GPL)
depends=(
  colord
  dconf
  gnome-desktop
  gnome-settings-daemon
  graphene
  gsettings-desktop-schemas
  lcms2
  libcanberra
  libei
  libgudev
  libinput
  libsm
  libsysprof-capture
  pipewire
)
makedepends=(
  cmake
  gnome-desktop-4
  egl-wayland
  gi-docgen
  git
  gobject-introspection
  libei
  libxcvt
  meson
  sysprof
  mold
  wayland-protocols
)
checkdepends=(
  python-dbusmock
  wireplumber
  xorg-server-xvfb
  zenity
)


source=(
  "https://gitlab.gnome.org/GNOME/mutter/-/archive/45.0/mutter-${pkgver}.tar.bz2"
)

sha512sums=('SKIP')  #mutter-45.0.tar.bz2

build() {
  CFLAGS="${CFLAGS/-fPIE/-fPIC}"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"

  arch-meson build mutter-${pkgver} \
    -D catch=false \
    -D cogl_tests=false \
    -D core_tests=false \
    -D clutter_tests=false \
    -D docs=false \
    -D egl=true \
    -D egl_device=true \
    -D gles2=true \
    -D glx=false \
    -D installed_tests=false \
    -D introspection=true \
    -D kvm_tests=false \
    -D libgnome_desktop=true \
    -D libwacom=false \
    -D native_backend=true \
    -D native_tests=false \
    -D remote_desktop=false \
    -D opengl=true \
    -D pango_ft2=true \
    -D profiler=false \
    -D tests=false \
    -D tty_tests=false \
    -D sm=false \
    -D startup_notification=false \
    -D sound_player=false \
    -D systemd=true \
    -D verbose=true \
    -D wayland=true \
    -D wayland_eglstream=true \
    -D xwayland=true \
    -D udev=true
    
  meson compile -C build
}

_check() (
  export XDG_RUNTIME_DIR="$PWD/rdir" GSETTINGS_SCHEMA_DIR="$PWD/build/data"
  mkdir -p -m 700 "$XDG_RUNTIME_DIR"
  glib-compile-schemas "$GSETTINGS_SCHEMA_DIR"

  pipewire &
  _p1=$!

  wireplumber &
  _p2=$!

  trap "kill $_p1 $_p2; wait" EXIT

  meson test -C build --print-errorlogs -t 3
)

check() {
  dbus-run-session xvfb-run -s '-nolisten local +iglx -noreset' \
    bash -c "$(declare -f _check); _check"
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_mutter() {
  provides=(libmutter-13.so)

  meson install -C build --destdir "$pkgdir"
}
