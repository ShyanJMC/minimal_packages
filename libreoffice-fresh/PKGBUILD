pkgbase=libreoffice-fresh-minimal
pkgname=('libreoffice-fresh-minimal')
_LOver=7.4.1.2
pkgver=7.4.1
pkgrel=1
arch=('x86_64')
license=('LGPL3')
url="https://www.libreoffice.org/"
makedepends=('gtk4' 'curl>=7.20.0' 'hunspell>=1.2.8' 'libwpd>=0.9.2' 'libwps'
	'neon>=0.28.6' 'pango' 'nspr' 'libjpeg' 'libxrandr' 'libgl' 'dbus-glib'
	'libxslt' 'redland' 'hyphen' 'lpsolve' 'gcc-libs' 'sh' 'graphite' 'icu' 
	'lcms2' 'poppler>=0.24.0' 'libvisio' 'libetonyek' 'libodfgen' 'libcdr'
	'libmspub' 'harfbuzz-icu' 'nss' 'hicolor-icon-theme'
	'desktop-file-utils' 'shared-mime-info' 'gst-plugins-base-libs'
	'gperf' 'gtk3' 'cppunit' 'clucene'
	'libmythes' 'libwpg'  'libgl'
	'bluez-libs' 'libatomic_ops'  'mdds'
	'apr' 'serf' 'ttf-liberation' 'ttf-dejavu' 'ttf-carlito' 'libxinerama' 'libpagemaker' 'glm'
	'libabw' 'libmwaw' 'libe-book' 'coin-or-mp' 'liblangtag' 'liborcus' 'libice' 'libsm'
	'libexttextcat' 'gobject-introspection' 
	'libtommath' 'libzmf' 'gpgme' 'rxvt-unicode' 
	'libepubgen' 'libfreehand' 'libqxp' 'libstaroffice'  'boost' 'libnumbertext'
	'libffi' 'box2d' 'git' 'zip' 'unzip'
)

_mirror="https://download.documentfoundation.org/libreoffice/src/${pkgver}"

source=(${_mirror}/libreoffice-${_LOver}.tar.xz
        soffice-template.desktop.in 
        libreoffice-fresh.sh libreoffice-fresh.csh)

validpgpkeys=('C2839ECAD9408FBE9531C3E9F434A1EFAFEEAEA3') # LibreOffice Build Team (CODE SIGNING KEY) <build@documentfoundation.org>

sha256sums=('92f78421bb3ffa6723f8e14c0dab5ee7bc68939fad8ff9f146ccecac02d2de84' #libreoffice-7.4.1.2.tar.xz
            'd0be8099cbee3c9dfda694a828149b881c345b204ab68826f317580aafb50879' #soffice-template.desktop.in
            'cd1b25ff390e436c6bffa65c6e12382236e3ccbf8d3aae51b1b59bcaed79fd8a' #libreoffice-fresh.sh
            'de20f36d45f0fecc2d94176dd3ec7226ab07fa8ffb9b0bc73c200349a9273de1') #libreoffice-fresh.csh


options=(strip !docs staticlibs !debug)

prepare() {

        cd libreoffice-$_LOver

        # Workaround to fix build with gpgme 1.18.0
        export ac_cv_lib_gpgmepp_progress_callback=yes

        # move external sources into place
        mkdir -p "${srcdir}"/ext_sources && pushd "${srcdir}"/ext_sources
        for source in "${noextract[@]}"; do
                ln -s "${srcdir}"/$source .
        done
        popd
        
        #use the CFLAGS but remove the LibO overridden ones
        for i in $CFLAGS; do
                case "$i" in
                        -O?|-pipe|-Wall|-g|-fexceptions) continue;;
                esac
                ARCH_FLAGS="$ARCH_FLAGS $i"
        done
}


build() {
		cd libreoffice-$_LOver

		./autogen.sh --with-extra-buildid="${pkgver}-${pkgrel}" \
			--with-vendor='ShyanJMC Linux' \
			--with-product-name='LibreOffice by ShyanJMC' \
		--enable-split-app-modules \
		--with-parallelism=${_MAKEFLAGS/-j/} \
		--with-external-tar="${srcdir}/ext_sources" \
		--enable-release-build \
		--prefix=/usr --exec-prefix=/usr --sysconfdir=/etc \
		--libdir=/usr/lib --mandir=/usr/local/man/man1/ \
		--with-help=no \
		--without-webdav \
		--disable-xmlhelp \
		--enable-wasm-exceptions=no \
		--enable-cipher-openssl-backend \
		--disable-extension-update \
		--with-omindex=no \
		--without-helppack-integration \
		--disable-cups \
		--disable-libcmis \
		--without-junit \
		--without-doxygen \
		--disable-librelogo \
		--disable-lotuswordpro \
		--disable-cve-tests \
		--disable-sdremote-bluetooth \
		--disable-sdremote \
		--disable-gstreamer-1-0 \
		--disable-gdb-index \
		--without-helppack-integration \
		--with-galleries=no \
		--enable-bundle-mariadb=no \
		--disable-ldap \
		--disable-firebird-sdbc \
		--disable-postgresql-sdbc \
		--disable-mariadb-sdbc \
		--enable-online-update=no \
		--enable-fuzzers=no \
		--disable-crashdump \
		--disable-report-builder \
		--without-lxml \
		--without-gssapi \
		--without-java \
		--disable-avahi \
		--enable-dbus \
		--disable-evolution2\
		--disable-gio\
		--disable-gtk3-kde5 \
		--disable-kf5 \
		--disable-qt5 \
		--enable-gtk4 \
		--enable-introspection \
		--enable-openssl \
		--disable-odk\
		--enable-python=no \
		--disable-scripting-beanshell \
		--disable-scripting-javascript \
		--disable-dconf \
		--disable-report-builder \
		--disable-ext-wiki-publisher \
		--disable-ext-nlpsolver \
		--with-external-dict-dir=/usr/share/hunspell \
		--with-external-hyph-dir=/usr/share/hyphen \
		--with-external-thes-dir=/usr/share/mythes \
		--disable-dependency-tracking 

	touch src.downloaded
	make build

	mkdir -p "${srcdir}"/fakeinstall
	make DESTDIR="${srcdir}"/fakeinstall distro-pack-install
}

package_libreoffice-fresh-minimal() {
	pkgdesc="LibreOffice branch which contains new features and program enhancements"
	depends=('curl>=7.20.0' 'hunspell>=1.2.8' 'libwpd>=0.9.2' 'libwps'
		'neon>=0.28.6' 'pango' 'nspr' 'libjpeg' 'libxrandr' 'libgl'
		'libxslt' 'redland' 'hyphen' 'lpsolve' 'gcc-libs' 'sh' 'graphite' 'icu' 
		'lcms2' 'poppler>=0.24.0' 'libvisio' 'libetonyek' 'libodfgen' 'libcdr'
		'libmspub' 'harfbuzz-icu' 'nss' 'clucene' 'hicolor-icon-theme'
		'desktop-file-utils' 'shared-mime-info' 'libpagemaker'
		'libxinerama' 'libabw' 'libmwaw' 'libe-book' 
		#'liblangtag' 'libexttextcat' 'libcmis' 'liborcus' # 'libfbclient'
		'liblangtag' 'libexttextcat' 'liborcus' # 'libfbclient'
		'libtommath' 'libzmf' 'libatomic_ops' 'xmlsec' 'gpgme' 'libnumbertext'
		'libfreehand' 'libstaroffice' 'libepubgen' 'libqxp' 'libepoxy' 'box2d'
		'xdg-utils')
	optdepends=('sane:             for scanner access'
		'gst-plugins-base-libs: for multimedia content, e.g. in Impress'
		'libpaper:         takes care of papersize'
		'coin-or-mp:	   required by the Calc solver'
		'gtk3:             for GTK3 integration'
		'kio:              for KF5 KDE desktop integration')
	backup=(etc/libreoffice/sofficerc
		etc/libreoffice/bootstraprc
		etc/libreoffice/psprint.conf
		etc/profile.d/libreoffice-fresh.sh
		etc/profile.d/libreoffice-fresh.csh)
	provides=('libreoffice' 'libreoffice-en-US')
	conflicts=('libreoffice-still')

	mv fakeinstall/* "${pkgdir}"/

	# put configuration files into place
	install -dm755 "${pkgdir}"/etc/libreoffice
	install -m644 "${pkgdir}"/usr/lib/libreoffice/program/{bootstraprc,sofficerc} \
		"${pkgdir}"/etc/libreoffice/
	install -m644 "${pkgdir}"/usr/lib/libreoffice/share/psprint/psprint.conf \
	   	"${pkgdir}"/etc/libreoffice/

	# install dummy links to make them found by LibO
	cd "${pkgdir}"/usr/lib/libreoffice/program/
	ln -vsf /etc/libreoffice/{bootstraprc,sofficerc} .
	cd "${pkgdir}"/usr/lib/libreoffice/share/psprint/
	ln -vsf /etc/libreoffice/psprint.conf .

	# allow to preset desired VLC
	install -dm755 "${pkgdir}"/etc/profile.d
	install -m644 "${srcdir}"/libreoffice-fresh.{sh,csh} "${pkgdir}"/etc/profile.d/

	# add a symlink required for gnome-documents; FS#51887
	# https://lists.freedesktop.org/archives/libreoffice/2016-March/073787.html
	ln -svf /usr/lib/libreoffice/program/liblibreofficekitgtk.so \
		"${pkgdir}"/usr/lib/liblibreofficekitgtk.so  

	# cleanup
	rm -rf "${pkgdir}"/usr/share/libreoffice/sdk

	# add kde filemanager templates; FS#61662 - file taken from Debian
	install -dm755 "${pkgdir}"/usr/share/templates/.source
	install -v -m644 "${srcdir}"/libreoffice-$_LOver/extras/source/shellnew/soffice.* \
		"${pkgdir}"/usr/share/templates/.source

	cat "${srcdir}"/soffice-template.desktop.in \
                | sed -e "s/@APP@/Writer/" \
                | sed -e "s/@EXT@/odt/" \
                | sed -e "s/@TYPE@/text/" \
                > "${pkgdir}"/usr/share/templates/soffice.odt.desktop
        cat "${srcdir}"/soffice-template.desktop.in \
                | sed -e "s/@APP@/Calc/" \
                | sed -e "s/@EXT@/ods/" \
                | sed -e "s/@TYPE@/spreadsheet/" \
                > "${pkgdir}"/usr/share/templates/soffice.ods.desktop
        cat "${srcdir}"/soffice-template.desktop.in \
                | sed -e "s/@APP@/Impress/" \
                | sed -e "s/@EXT@/odp/" \
                | sed -e "s/@TYPE@/presentation/" \
                > "${pkgdir}"/usr/share/templates/soffice.odp.desktop
        cat "${srcdir}"/soffice-template.desktop.in \
                | sed -e "s/@APP@/Draw/" \
                | sed -e "s/@EXT@/odg/" \
                | sed -e "s/@TYPE@/drawing/" \
                > "${pkgdir}"/usr/share/templates/soffice.odg.desktop

}
