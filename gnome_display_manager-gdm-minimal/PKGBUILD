# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

# Mod: Joaquin 'ShyanJMC' Crespo <shyan@shyanjmc.com>

pkgbase=gdm
pkgname=(gdm libgdm)
pkgver=44.1
pkgrel=1
pkgdesc="Display manager and login screen"
url="https://wiki.gnome.org/Projects/GDM"
arch=(x86_64)
license=(GPL)
depends=(gnome-shell gnome-session upower xorg-xrdb xorg-server xorg-xhost wayland xorg-xwayland 
         libxdmcp systemd libcanberra)
makedepends=(gobject-introspection git meson itstool libxml2)
checkdepends=(check)
options=(!debug strip staticlibs !docs)
_commit=6f137e9b59a0bb4b5a47d4af723c64bce053ea1d
source=("https://gitlab.gnome.org/GNOME/gdm/-/archive/${pkgver}/gdm-${pkgver}.tar.bz2")
sha256sums=( 'de89ae8fceb0d781b71da9fb05609a0ae78c10a223b3e707458c251e198e3ec5' )

build(){
  	local meson_options=(
    -D dbus-sys="/usr/share/dbus-1/system.d"
    -D default-pam-config=arch
    -D default-path="/usr/local/bin:/usr/local/sbin:/usr/bin"
    -D gdm-xsession=true 			# Xorg support
    -D ipv6=false					# Remote access trough gdm support with ipv6
    -D profiling=false
    -D solaris=false				# solarisOS
    -D libaudit=disabled
    -D split-authentication=false	
    -D tcp-wrappers=false			# Remote access trough gdm
    -D plymouth=disabled			# Playmouth theme support
    -D run-dir=/run/gdm
    -D selinux=disabled				# SELinux kernel support
    -D xdmcp=enabled					# Xorg support
    -D user-display-server=true		# Gnome-shell user mode support
    -D wayland-support=true			# Wayland support
    )

  	arch-meson gdm-${pkgver} build "${meson_options[@]}"
  	meson compile -C build -j $(nproc)
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_gdm() {
  depends+=(libgdm)
  optdepends=('fprintd: fingerprint authentication')
  backup=(etc/pam.d/gdm-autologin etc/pam.d/gdm-fingerprint etc/pam.d/gdm-launch-environment
          etc/pam.d/gdm-password etc/pam.d/gdm-smartcard etc/gdm/custom.conf
          etc/gdm/Xsession etc/gdm/PostSession/Default etc/gdm/PreSession/Default)
  groups=(gnome)
  install=gdm.install

  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"

  install -d -o   0 -g   0 -m 0755 var
  install -d -o   0 -g   0 -m 0755 var/lib
  install -d -o 120 -g 120 -m 1770 var/lib/gdm
  install -d -o 120 -g 120 -m 0700 var/lib/gdm/.config
  install -d -o 120 -g 120 -m 0700 var/lib/gdm/.config/pulse
  install -d -o 120 -g 120 -m 0700 var/lib/gdm/.local
  install -d -o 120 -g 120 -m 0755 var/lib/gdm/.local/share
  install -d -o 120 -g 120 -m 0755 var/lib/gdm/.local/share/applications

  # https://src.fedoraproject.org/rpms/gdm/blob/master/f/default.pa-for-gdm
  install -o120 -g120 -m644 /dev/stdin var/lib/gdm/.config/pulse/default.pa <<END
load-module module-device-restore
load-module module-card-restore
load-module module-udev-detect
load-module module-native-protocol-unix
load-module module-default-device-restore
load-module module-always-sink
load-module module-intended-roles
load-module module-suspend-on-idle
load-module module-systemd-login
load-module module-position-event-sounds
END

  install -Dm644 /dev/stdin usr/lib/sysusers.d/gdm.conf <<END
g gdm 120 -
u gdm 120 "Gnome Display Manager" /var/lib/gdm
END

  _pick libgdm usr/include
  _pick libgdm usr/lib/{girepository-1.0,libgdm*,pkgconfig}
  _pick libgdm usr/share/{gir-1.0,glib-2.0}
}

package_libgdm() {
  pkgdesc+=" - support library"
  depends=(libsystemd.so libg{lib,object,io}-2.0.so)
  provides=(libgdm.so)

  mv libgdm/* "$pkgdir"
}
