# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

# Mod: Joaquin 'ShyanJMC' Crespo <shyan@shyanjmc.com>

pkgname=gnome-disk-utility
pkgver=44.0
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-disk-utility"
arch=(x86_64)
license=(GPL)
depends=(
  gtk4
  libcanberra
  libdvdread
  libhandy
  libnotify
  libpwquality
  libsecret
  parted
  systemd
  udisks2
)
makedepends=(
  meson
)
groups=(gnome)
source=("https://gitlab.gnome.org/GNOME/gnome-disk-utility/-/archive/${pkgver}/gnome-disk-utility-${pkgver}.tar.bz2")
sha256sums=('f1f1f6fd2537d81d0489c8bdc8ef38678ee63bcbfd324d4c5fa8350ae70254df')

build() {
  local meson_options=(
    -D logind=none
    -D gsd_plugin=false
    -D man=false
  )

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  arch-meson gnome-disk-utility-${pkgver} build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
